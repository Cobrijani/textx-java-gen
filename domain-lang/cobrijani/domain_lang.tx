/*
  Entity DSL grammar.
*/
EntityModel:
    types*=SimpleType       // At the beginning of model we can define
                            // zero or more simple types.
    entities+=Entity        // Each model has one or more entities.
;

Entity:
    'entity' name=ID '{'
        properties+=Property // Each entity has one or more properties.

        relationships*=Relationship
    '}'
;

Relationship:
    name=ID '->' ent=[Entity] card=Cardinality
;

OneToOne:
  '[1]'| ''
;
OneToOneNil:
  '[0..1]'
;
OneToMany:
  '[0..n]'
;
ManyToOne:
  '[n..0]'
;
Cardinality:
    OneToOneNil | OneToMany | ManyToOne | OneToOne
;

Property:
    name=ID ':' type=[PropertyType]
                                // type is a reference to Type instance.
                                // There are two built-in simple types
                                // registered on meta-model in entity_test.py
;
// Type can be SimpleType or Entity
PropertyType:
    SimpleType
;

SimpleType:
    'type' name=ID
;

// Special rule for comments. Comments start with //
Comment:
    /\/\/.*$/
;
